apply plugin: 'com.android.library'
android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 3
        versionName "3.0.3"
        resConfigs "en", "hdpi"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'InflateParams', 'SetJavaScriptEnabled'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    dependencies {
        compile fileTree(dir: 'libs', include: ['*.jar'])
        compile 'com.android.support:appcompat-v7:24.2.1'
        compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
        compile 'com.squareup:otto:1.3.8'
        compile (
                [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.7'],
                [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.6.7'],
                [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.7']

        )

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.2.0'
        testCompile "org.robolectric:robolectric:3.1.2"
        testCompile 'org.slf4j:slf4j-log4j12:1.7.21'

        androidTestCompile 'com.android.support.test:runner:0.5'
        androidTestCompile 'com.android.support.test:rules:0.5'
        androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
        androidTestCompile 'org.mockito:mockito-core:1.10.19'
        androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
        androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    }
}
def coverageSourceDirs = [
        'src/main/java',
]
apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}



task jacocoCombinedTestReport(type: JacocoReport, dependsOn: ["testDebugUnitTest", "createDebugCoverageReport"]) {

    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled = true
        html.destination "${buildDir}/jacocoHtml"
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    additionalSourceDirs = files(coverageSourceDirs)
    executionData = files(["$buildDir/jacoco/testDebugUnitTest.exec",
                           "$buildDir/outputs/code-coverage/connected/Nexus_5X_API_23(AVD) - 6.0-coverage.ec"])

    onlyIf = {
        true
    }

    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}
dependencies {
}